/*
   Copyright 2021 Snowplow Analytics Ltd. All rights reserved.
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

config {
  type: "assertion",
  disabled: sp.assertions_disabled || sp.model_disabled,
  database: dataform.projectConfig.defaultDatabase,
  name: "022_page_view_in_session_values",
  dependencies: ["99_users_cleanup"],
  hermetic: true,
  tags: ["sp_assert"]
}

WITH
  prep AS (
    SELECT
      domain_sessionid,
      count(DISTINCT page_views_in_session) AS dist_pvis_values,
      count(*) - count(DISTINCT page_view_in_session_index)  AS all_minus_dist_pvisi,
      count(*) - count(DISTINCT page_view_id) AS all_minus_dist_pvids

    FROM
      `${sp.output_schema}.page_views${sp.entropy}`

    GROUP BY 1
  )
  SELECT
    domain_sessionid

  FROM prep

  WHERE dist_pvis_values != 1
    OR all_minus_dist_pvisi != 0
    OR all_minus_dist_pvids != 0
