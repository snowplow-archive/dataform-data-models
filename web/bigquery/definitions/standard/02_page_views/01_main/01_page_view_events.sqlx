/*
   Copyright 2021 Snowplow Analytics Ltd. All rights reserved.
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

config {
  type: "operations",
  name: "01_page_view_events",
  disabled: sp.model_disabled,
  dependencies: ["00_setup_page_views"],
  hermetic: true,
  hasOutput: false,
  tags: ["sp_web", "sp_page_views"]
}

js {
  function if_iab_filter() {
    if (sp.ua_bot_filter === true) {
      return `
        WHERE NOT REGEXP_CONTAINS(ev.useragent, '%(bot|crawl|slurp|spider|archiv|spinn|sniff|seo|audit|survey|pingdom|worm|capture|(browser|screen)shots|analyz|index|thumb|check|facebook|PingdomBot|PhantomJS|YandexBot|Twitterbot|a_archiver|facebookexternalhit|Bingbot|BingPreview|Googlebot|Baiduspider|360(Spider|User-agent)|semalt)%')`;
    } else {
      return ``;
    }
  }

}

CREATE OR REPLACE TABLE `${sp.scratch_schema}.pv_page_view_events${sp.entropy}`
AS(
  SELECT
    ev.page_view_id,
    ev.event_id,

    ev.app_id,

    -- user fields
    ev.user_id,
    ev.domain_userid,
    ev.network_userid,

    -- session fields
    ev.domain_sessionid,
    ev.domain_sessionidx,

    -- timestamp fields
    ev.dvce_created_tstamp,
    ev.collector_tstamp,
    ev.derived_tstamp,
    ev.derived_tstamp AS start_tstamp,

    ev.doc_width,
    ev.doc_height,

    ev.page_title,
    ev.page_url,
    ev.page_urlscheme,
    ev.page_urlhost,
    ev.page_urlpath,
    ev.page_urlquery,
    ev.page_urlfragment,

    ev.mkt_medium,
    ev.mkt_source,
    ev.mkt_term,
    ev.mkt_content,
    ev.mkt_campaign,
    ev.mkt_clickid,
    ev.mkt_network,

    ev.page_referrer,
    ev.refr_urlscheme ,
    ev.refr_urlhost,
    ev.refr_urlpath,
    ev.refr_urlquery,
    ev.refr_urlfragment,
    ev.refr_medium,
    ev.refr_source,
    ev.refr_term,

    ev.geo_country,
    ev.geo_region,
    ev.geo_region_name,
    ev.geo_city,
    ev.geo_zipcode,
    ev.geo_latitude,
    ev.geo_longitude,
    ev.geo_timezone ,

    ev.user_ipaddress,

    ev.useragent,

    ev.br_lang,
    ev.br_viewwidth,
    ev.br_viewheight,
    ev.br_colordepth,
    ev.br_renderengine,
    ev.os_timezone,

    ROW_NUMBER() OVER (PARTITION BY ev.domain_sessionid ORDER BY ev.derived_tstamp) AS page_view_in_session_index

  FROM (
    SELECT
      ARRAY_AGG(e ORDER BY e.derived_tstamp LIMIT 1)[OFFSET(0)] AS ev
      -- order by matters here since derived_tstamp determines parts of model logic

    FROM `${sp.scratch_schema}.events_staged${sp.entropy}` e
    WHERE e.event_name = 'page_view'
    AND e.page_view_id IS NOT NULL
    GROUP BY e.page_view_id
  )

  ${if_iab_filter()}

);
