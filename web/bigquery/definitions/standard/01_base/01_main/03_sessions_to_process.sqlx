/*
   Copyright 2021 Snowplow Analytics Ltd. All rights reserved.
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

config {
  type: "operations",
  name: "03_sessions_to_process",
  disabled: sp.model_disabled,
  dependencies: ["02_run_manifest"],
  hermetic: true,
  hasOutput: false,
  tags: ["sp_web", "sp_base"]
}

js {
  function if_derived_partitioned() {
    if (sp.derived_tstamp_partitioned === true) {
      return `
        AND a.derived_tstamp >= LOWER_LIMIT
        AND a.derived_tstamp <= UPPER_LIMIT
      `;
    } else {
      return '';
    }
  }
}

-- Use variable to set scan limits
DECLARE LOWER_LIMIT, UPPER_LIMIT TIMESTAMP;

SET (LOWER_LIMIT, UPPER_LIMIT) = (SELECT AS STRUCT lower_limit, upper_limit FROM `${sp.scratch_schema}.base_new_events_limits${sp.entropy}`);

-- Get sessionids for new events
CREATE OR REPLACE TABLE `${sp.scratch_schema}.base_sessions_to_process${sp.entropy}`
AS(
  SELECT
    domain_sessionid AS session_id,
    MIN(a.collector_tstamp) AS min_tstamp,
    MAX(a.collector_tstamp) AS max_tstamp

  FROM
    `${sp.input_schema}.events` a
  LEFT JOIN
    `${sp.scratch_schema}.base_run_manifest${sp.entropy}` b
    ON a.event_id = b.event_id

  WHERE
    b.event_id IS NULL
    AND a.collector_tstamp >= LOWER_LIMIT
    AND a.collector_tstamp <= UPPER_LIMIT
    AND a.domain_sessionid IS NOT NULL
    AND TIMESTAMP_DIFF(a.dvce_sent_tstamp, a.dvce_created_tstamp, DAY) <= ${sp.days_late_allowed}
    -- don't process data that's too late

    ${if_derived_partitioned()}

  GROUP BY 1
);
