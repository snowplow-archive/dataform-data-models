/*
   Copyright 2021 Snowplow Analytics Ltd. All rights reserved.
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

config {
  type: "operations",
  name: "06_commit_sessions",
  disabled: sp.model_disabled,
  dependencies: ["05_sessions_prep_manifest"],
  hermetic: true,
  hasOutput: false,
  tags: ["sp_web", "sp_sessions"]
}

js {
  function if_derived_sessions() {
    if (sp.skip_derived.sessions === false) {
      return `
        -- Commit to production if enabled
        -- Note: Automigrate hardcoded to false as all columns are to be explicitly defined in model.
        CALL \`${sp.output_schema}.commit_table\` (
          '${sp.scratch_schema}',             -- sourceDataset
          'sessions_this_run${sp.entropy}',   -- sourceTable
          '${sp.output_schema}',              -- targetDataset
          'sessions${sp.entropy}',            -- targetTable
          'domain_sessionid',                 -- joinKey
          'start_tstamp',                     -- partitionKey
          FALSE);                             -- automigrate
      `;
    } else {
      return ``;
    }
  }

  function if_stage_sessions() {
    if (sp.stage_next.sessions === true) {
      return `
        -- Commit staging manifest if enabled
        DELETE FROM \`${sp.scratch_schema}.sessions_userid_manifest_staged${sp.entropy}\`
        WHERE domain_userid IN (SELECT domain_userid FROM \`${sp.scratch_schema}.sessions_userid_manifest_this_run${sp.entropy}\`);

      INSERT INTO \`${sp.scratch_schema}.sessions_userid_manifest_staged${sp.entropy}\`
        (SELECT * FROM \`${sp.scratch_schema}.sessions_userid_manifest_this_run${sp.entropy}\`);
      `;
    } else {
      return ``;
    }
  }
}

BEGIN

  ${if_derived_sessions()}

  ${if_stage_sessions()}

  -- Commit metadata
  INSERT INTO `${sp.output_schema}.datamodel_metadata${sp.entropy}` (
    SELECT
      run_id,
      model_version,
      model,
      module,
      run_start_tstamp,
      CURRENT_TIMESTAMP() AS run_end_tstamp,
      rows_this_run,
      distinct_key,
      distinct_key_count,
      time_key,
      min_time_key,
      max_time_key,
      duplicate_rows_removed,
      distinct_keys_removed

    FROM `${sp.scratch_schema}.sessions_metadata_this_run${sp.entropy}`
  );

END;
