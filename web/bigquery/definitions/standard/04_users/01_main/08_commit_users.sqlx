/*
   Copyright 2021 Snowplow Analytics Ltd. All rights reserved.
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

config {
  type: "operations",
  name: "08_commit_users",
  disabled: sp.model_disabled,
  dependencies: ["07_users_metadata"],
  hermetic: true,
  hasOutput: false,
  tags: ["sp_web", "sp_users"]
}

js {
  function if_derived_users() {
    if (sp.skip_derived.users === false) {
      return `
        -- Commit to production if enabled
        -- Note: Automigrate hardcoded to false as all columns are to be explicitly defined in model.
        CALL \`${sp.output_schema}.commit_table\` (
          '${sp.scratch_schema}',          -- sourceDataset
          'users_this_run${sp.entropy}',   -- sourceTable
          '${sp.output_schema}',           -- targetDataset
          'users${sp.entropy}',            -- targetTable
          'domain_userid',                 -- joinKey
          'start_tstamp',                  -- partitionKey
          FALSE);                          -- automigrate
      `;
    } else {
      return ``;
    }
  }
}

BEGIN

  ${if_derived_users()}

  -- Commit metadata
  INSERT INTO `${sp.output_schema}.datamodel_metadata${sp.entropy}` (
    SELECT
      run_id,
      model_version,
      model,
      module,
      run_start_tstamp,
      CURRENT_TIMESTAMP() AS run_end_tstamp,
      rows_this_run,
      distinct_key,
      distinct_key_count,
      time_key,
      min_time_key,
      max_time_key,
      duplicate_rows_removed,
      distinct_keys_removed
    FROM `${sp.scratch_schema}.users_metadata_this_run${sp.entropy}`
  );

END;
